# This token is used to bootstrap the cluster and join new nodes
resource "random_string" "rke2_token" {
  length = 64
}

locals {
  # configure rke2_server_url to controlplane_hostname if set, else to the ipv4 of the controlplane load balancer
  rke2_server_url = "https://${var.controlplane_hostname != null ? var.controlplane_hostname : hcloud_load_balancer.controlplane.ipv4}:9345"


  # Configure both the controlplane loadbalancers' IPv4 and IPv6 addresses as
  # SANs, and optionally the controlplane_hostname if set.
  rke2_tls_san = concat(
    [hcloud_load_balancer.controlplane.ipv4, hcloud_load_balancer.controlplane.ipv6],
    var.controlplane_hostname != null ? [var.controlplane_hostname] : []
  )

  # TODO: internal networking!

  userdata_server_bootstrap = module.rke2_cloudconfig_server_bootstrap.userdata
  userdata_server           = module.rke2_cloudconfig_server.userdata
  userdata_agent            = module.rke2_cloudconfig_agent.userdata

  k8s_extra_config = {
    kubelet-arg = var.setup_hetzner_ccm ? [
      "cloud-provider=external"
    ] : []
  }

  k8s_extra_config_server = merge(local.k8s_extra_config, {
    # This configures kube-apiserver to prefer InternalIP over everything
    # TODO(arianvp): open issue
    # This arg is ignored by rke2 agents, so we don't need to conditionalize
    # k8s_extra_config.
    kube-apiserver-arg = [
      "kubelet-preferred-address-types=InternalIP,ExternalIP,Hostname"
    ]
  })

  # Before running the installation script, but after cloud-init already provided the
  # /etc/rancher/rke2/config.yaml file, annotate it with the internal ip
  # adresses retrieved from the hcloud metadata server.
  # This is somewhat terrifying, but given there's no yq available on fedora,
  # use some bash :-)
  install_script_pre = <<-EOC
    internal_ip=$(curl -sfL http://169.254.169.254/hetzner/v1/metadata/private-networks | grep "ip:" | head -n 1| cut -d ":" -f2 | xargs)
    echo "node-ip: $internal_ip" >> /etc/rancher/rke2/config.yaml
  EOC

  # We can't provide additional files in /var/lib/rancher/rke2/server/manifests during startup,
  # as these get overwritten on startup apparently
  # Until addons are supported in RKE2
  # (https://github.com/rancher/rke2/issues/568), we drop it in
  # /var/lib/rancher/custom_rke2_addons, which is a poormans alternative to it.
  install_script_post = join("\n", [
    # This installs the Hetzner Cloud Controller Manager if enabled (the default)
    var.setup_hetzner_ccm ? <<-EOQ
      curl -sfL https://raw.githubusercontent.com/hetznercloud/hcloud-cloud-controller-manager/v1.8.1/deploy/ccm.yaml > /var/lib/rancher/custom_rke2_addons/hetzner_ccm.yaml
      cat << EOG > /var/lib/rancher/custom_rke2_addons/nginx-use-loadbalancer.yaml
      apiVersion: helm.cattle.io/v1
      kind: HelmChartConfig
      metadata:
        name: rke2-ingress-nginx
        namespace: kube-system
      spec:
        valuesContent: |-
          controller:
            kind: Deployment
            autoscaling:
              enabled: true
              minReplicas: 2
              maxReplicas: 5
            hostNetwork: false
            service:
              enabled: true
              type: LoadBalancer
              externalTrafficPolicy: Local
              annotations:
                load-balancer.hetzner.cloud/location: nbg1
      EOG
    EOQ
    : ""
  ])
}

module "rke2_cloudconfig_server_bootstrap" {
  source              = "./cloudconfig-rke2"
  rke2_token          = random_string.rke2_token.result
  server_tls_san      = local.rke2_tls_san
  node_taint          = (! var.controlplane_has_worker) ? ["CriticalAddonsOnly=true:NoExecute"] : []
  install_rke2_type   = "server"
  install_script_pre  = local.install_script_pre
  install_script_post = local.install_script_post
  extra_config        = local.k8s_extra_config_server
}

module "rke2_cloudconfig_server" {
  source              = "./cloudconfig-rke2"
  rke2_token          = random_string.rke2_token.result
  server_tls_san      = local.rke2_tls_san
  node_taint          = (! var.controlplane_has_worker) ? ["CriticalAddonsOnly=true:NoExecute"] : []
  install_rke2_type   = "server"
  server_url          = local.rke2_server_url
  install_script_pre  = join("\n", [local.install_script_pre, "sleep 200"])
  install_script_post = local.install_script_post
  extra_config        = local.k8s_extra_config_server
}

module "rke2_cloudconfig_agent" {
  source              = "./cloudconfig-rke2"
  rke2_token          = random_string.rke2_token.result
  install_rke2_type   = "agent"
  server_url          = local.rke2_server_url
  install_script_pre  = join("\n", [local.install_script_pre, "sleep 200"])
  install_script_post = local.install_script_post
  extra_config        = local.k8s_extra_config
}

